#!/bin/sh

. pipestatus

sumcounts (){
    @AWK@ '
/^info / {
    print
    next
}

function get_id (st){
    if (st in ids){
	return ids [st]
    }else{
	ids [st] = ++ids_count
	return ids_count
    }
}

/^malloc / {
    id = get_id($7)
    ptr2trace [$6]   = id
    ptr2size  [$6]   = $3
    allocated [id]  += $3
    total_allocated += $3
    ++total_allocs_cnt
    if (allocated [id] > peak_allocated [id])
	peak_allocated [id] = allocated [id]
    if ($3+0 > max_allocated [id])
	max_allocated [id] = $3
    ++allocs_cnt [id]
    next
}

/^calloc / {
    id = get_id($9)
    bytes = $3 * $5
    ptr2trace [$8]   = id
    ptr2size  [$8]   = bytes
    allocated [id]  += bytes
    total_allocated += bytes
    ++total_allocs_cnt
    if (allocated [id] > peak_allocated [id])
	peak_allocated [id] = allocated [id]
    if (bytes > max_allocated [id])
	max_allocated [id] = bytes
    ++allocs_cnt [id]
    next
}

/^(memalign|posix_memalign) / {
    id = get_id($9)
    ptr2trace [$8]   = id
    ptr2size  [$8]   = $5
    allocated [id]  += $5
    total_allocated += $5
    ++total_allocs_cnt
    if (allocated [id] > peak_allocated [id])
	peak_allocated [id] = allocated [id]
    if ($5+0 > max_allocated [id])
	max_allocated [id] = $5
    ++allocs_cnt [id]
    next
}

/^realloc / {
    id = get_id($9)
    ptr2trace [$8]   = id
    ptr2size  [$8]   = $5
    allocated [id]  += $5
    total_allocated += $5
    total_allocated            -= ptr2size [$3]
    allocated [ptr2trace [$3]] -= ptr2size [$3]
    ++total_allocs_cnt
    if (allocated [id] > peak_allocated [id])
	peak_allocated [id] = allocated [id]
    if ($5+0 > max_allocated [id])
	max_allocated [id] = $5
    ++allocs_cnt [id]
    next
}

/^free / {
    total_allocated            -= ptr2size [$3]
    allocated [ptr2trace [$3]] -= ptr2size [$3]
    ptr2size [$3] = 0
    ++total_free_cnt
    next
}

END {
    print "info stat total_leaks: " total_allocated
    print "info stat total_allocs: " total_allocs_cnt
    print "info stat total_free_cnt: " total_free_cnt

    for (i in ids){
	printf "stacktrace"
	id = get_id(i)
	if ((id in allocated) && allocated [id])
	    printf " leaks: %s", allocated [id]
	if ((id in peak_allocated) && peak_allocated [id])
	    printf " peak: %s", peak_allocated [id]
	if (id in max_allocated)
	    printf " max: %s", max_allocated [id]
	if (id in allocs_cnt)
	    printf " allocs: %s", allocs_cnt [id]
	printf " %s\n", i
    }
}
' "$@"
}

runpipe0 \
    lmdbg-m2s "$@" '|' \
    sumcounts '|' \
    lmdbg-s2m
