#!/usr/bin/env runawk

#use "power_getopt.awk"

#env "LC_ALL=C"

#.begin-str help
# lmdbg-strip - strip stacktraces given on input
# usage: lmdbg-strip [OPTIONS] [files...]
# OPTIONS:
#   -h|--help     display this screen
#   -a            remove stacktrace addresses
#   -r            replace return and input addresses with fake value
#   -l            remove line number from sourcefile.c:<linenumber>
# Option -c is mandatory.
#.end-str help

BEGIN {
	mode_a = getarg("a")
	mode_r = getarg("r")
	mode_l = getarg("l")

	if (!mode_a && !mode_l && !mode_r){
		print "At least of the follosing options must be specified: -a, -l or -r" > "/dev/stderr"
		exitnow(1)
	}

	FS = OFS = "\t"
}

mode_a && /^ / {
    if (NF == 1) next
	$1 = " "
}

mode_r && /^[^ ]/ {
	if (/^(malloc|calloc|memalign|posix_memalign|free) /){
		sub(/0x[0-9A-Fa-f]+/, "0xXYZ")
	}else if (/^realloc /){
		sub(/0x[0-9A-Fa-f]+/, "0xXYZ")
		sub(/0x[0-9A-Fa-f]+/, "0xXYZ")
	}
}

mode_l && NF >= 2 {
	sub(/:[^: \t]+$/, "", $2)
}

{
	print 
}
